// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

	// on if A instruction i.e. msb is zero
	Not(in=instruction[15], out=ainstr);

	// load Dreg and Areg if C-instruction and dest bit is on
	And(a=instruction[15], b=instruction[3], out=writeM);
	And(a=instruction[15], b=instruction[4], out=dload);
	And(a=instruction[15], b=instruction[5], out=aaload);

	// load a if A-instruction or C-instruction and destination bit is on
	Or(a=ainstr, b=aaload, out=aload);	

	DRegister(in=aluout, load=dload, out=dregout);

	ARegister(in=mux1out, load=aload, out=aregout, out=aregout2, out[0..14]=addressM);

	Mux16(a=instruction, b=aluout, sel=instruction[15], out=mux1out);
	Mux16(a=aregout, b=inM, sel=instruction[12], out=mux2out);

	ALU(x=dregout, y=mux2out, zr=zrout, ng=ngout, zx=instruction[11], nx=instruction[10], 
		zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluout, out=outM);
	
	// Jump logic
	And(a=zrout, b=instruction[1], out=iszero);
	And(a=ngout, b=instruction[2], out=isneg);
	Not(in=ngout, out=not1);
	Not(in=zrout, out=not2);
	And(a=not1, b=not2, out=andout);
	And(a=andout, b=instruction[0], out=ispos);
	Or(a=iszero, b=isneg, out=jump1);
	Or(a=jump1, b=ispos, out=jump2);
	And(a=jump2, b=instruction[15], out=jump);

	PC(inc=true, load=jump, reset=reset, in=aregout2, out[0..14]=pc);
	
}
